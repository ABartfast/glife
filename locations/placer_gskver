# placer_gskver
if hour >= 22 or hour < 8:
	encounter = 0
else
	encounter = rand(0, 5)
end

if encounter = 0:
	!!no one in the park
	$textEvent = 'In the park is quiet and deserted. No one in sight.'
elseif encounter = 1:
	!!people in the park 1mother walking with a baby 2old_man 3woman with shopping bags 4two young girls
	encrand = rand(0, 4)

	if encrand = 0:$textEvent = 'In the park on the sidewalk strolls a young mother with a stroller.'
	if encrand = 1 and cloth_vid = 0:$textEvent = 'In the park on the sidewalk walks a lonely old man with a newspaper in his hand.'

	if encrand = 1 and cloth_vid = 1:
		$textEvent='In a park on the sidewalk walking a lonely old man with a newspaper in his hand. The old man came up to you and said: "Look, young lady, I have something for you-whats is"'

		act 'see':gt 'parkBimbo','pos1'
	end

	if encrand = 2:$textEvent = 'In the park on the sidewalk is a woman with shopping bags who decided to shorten the path passing through the square.'
	if encrand = 3:$textEvent = 'In the park on the sidewalk stroll two young girls.'

	if encrand = 4:
		kol_man = rand(1, 5)

		if kol_man = 1:$textSub = 'is a guy'
		if kol_man = 2:$textSub = 'are two guys'
		if kol_man = 3:$textSub = 'are three guys'
		if kol_man = 4:$textSub = 'four guys'
		if kol_man = 5:$textSub = 'five guys'

		$textEvent = 'In the park on the sidewalk <<$textSub>>.'
		mass_ver_zn = 1
		know_status = 1
	end
elseif encounter = 2:
	!!dog in the park
	$textEvent = 'In the park is quiet and deserted only lonely dog ​​sniffs to something near the boardwalk.'
elseif encounter = 3:
	!!young people in the park company
	$textEvent = 'In the park on a bench sits youth campaign.'
	kol_man = rand(2, 6)
	mass_ver_zn = 1
	know_status = 1
elseif encounter = 4:
	!!Gopnik in the park
	kol_man = rand(3, 6)
	$textEvent = 'In the park sitting on his haunches Gopnik and drink beer.'
	mass_ver_zn = 1
	know_status = 2
elseif encounter = 5:
	!!couple in love in the park
	$textEvent = 'In the park walks a couple in love.'
elseif encounter = 6:
	!!in the square of the familiar class
end

if dickfrend > 0:ver_zn = 0

gs 'placer_man'

if hour >= 8 and hour < 22:'<center><img src="images/gevent/skver.jpg"></center>'
if hour >= 22 or hour < 8:'<center><img src="images/gevent/skver.jpg"></center>'

if Frend_num > 0:
	'together with <<$ev_name_om>> you have come to a quiet garden square. <<$textEvent>>'

	if dickfrend > 0:
		!!your friend guy
		if kol_man = 1 and $text_znak = '':$kol_man_text_no = 'After the guy looked at you, he turned away.'
		if kol_man > 1 and $text_znak = '':$kol_man_text_no = 'Once you guys are inspected, they turned their backs.'
		if kol_man > 0:'You looked <<$kol_man_prefiks>> appreciatively <<$kol_man_text>>. <<$kol_man_text_no>>.'+$text_znak
		!!action with a guy whom came to the square
	elseif dickfrend = 0:
		!!your friend girl
		if ver_zn = 1:
			'At you with his girlfriend looked <<$kol_man_prefiks>> appreciatively <<$kol_man_text>>.'

			if kol_man = 1:'A guy came up to you and said, "<<$text_znak>>"'
			if kol_man > 1:'Guys come to you and one of the guys said "<<$text_znak>>"'

			act 'meet':
				cls

				if p_dom < 80:
					gt 'placer_act'
				elseif p_dom >= 80:
					gs 'stat'
					'<<$ev_name>> hissed in your ear "I''m not acquainted with someone on the street. If you want to, stay, and I will go."'

					act 'Go with a friend':gt 'placer_end'

					act 'can you go, and I''ll stay':
						Frend_num = 0
						gt 'placer_act'
					end
				end
			end
		elseif ver_zn = 0 and kol_man > 0:
			if kol_man = 1:$kol_man_text_no = 'After the guy looked at you, he turned away.'
			if kol_man > 1:$kol_man_text_no = 'Once you guys are inspected, they turned their backs.'

			'At you with his girlfriend looked <<$kol_man_prefiks>> appreciatively <<$kol_man_text>>. <<$kol_man_text_no>>.'
		end
		!!acts with the girl you came
	end

	act 'Continue to walk':gt 'placer_end'
elseif Frend_num = 0:
	'You stroll through the quiet park. <<$textEvent>>'

	if ver_zn = 1:
		'You looked <<$kol_man_prefiks>> appreciatively <<$kol_man_text>>.'

		if kol_man = 1:'The guy came up to you and said, "<<$text_znak>>"'
		if kol_man > 1:'Guys come to you and one of the guys said "<<$text_znak>>"'

		act 'meet':gt 'placer_act'
	elseif ver_zn = 0 and kol_man > 0:
		if kol_man = 1:$kol_man_text_no = 'After the guy looked at you, he turned away.'
		if kol_man > 1:$kol_man_text_no = 'Once you guys are inspected, they turned their backs.'

		'You looked <<$kol_man_prefiks>> appreciatively <<$kol_man_text>>. <<$kol_man_text_no>>.'
	end

	act 'Leave':gt 'gskver'
end

gs 'stat'

--- placer_gskver ---------------------------------

# kikbox
if $ARGS[0] = 'start':
	'<center><b>Round 1</b></center>'
	*nl
	'<center><img src="images/community/ring1.jpg"></center>'

	act 'Start Fight':
		gt 'kikbox', 'fight'
	end
end

if $ARGS[0] = 'fight':
	gs 'befKik'
	'<b>======================== (ROUND <<round>>) ===========================</b>'
	if minround = 1:'<b>the fight 0:30</b>'
	if minround = 2:'<b>the fight 1:00</b>'
	if minround = 3:'<b>the fight 1:30</b>'
	if minround = 4:'<b>the fight 2:00</b>'
	if minround = 5:'<b>the fight 2:30</b>'
	'<b><<$nameV>></b> Life <b><font color="red"><<healthV>></font></b>, Endurance <b><font color="green"><<staminaV>></font></b>'
	'Your life <b><font color="red"><<health>></font></b>, Endurance <b><font color="green"><<stamina>></font></b>'
	'<b>==========================================================</b>'
	'Your points <<pointKik>>Points opponent <<pointKikV>>'
	'<b>==========================================================</b>'
	gs 'initKik'

	if hod = 0:
		!!Player attacks
		'<font color="green">You can attack</font>'

		act 'Apply fast Jeb':
			cla
			set damage = rand(stren/12, stren/8)

			if damage <= 0:set damage = 1

			set koefatak = (speed + reakt + jab) * 2
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="green">You deal with a quick jab step forward.</font>'
			gs 'Kikatak', 'player'
			xgt 'kikbox', 'sta'
		end

		act 'Apply power punch':
			cla
			set damkoef = stren * 20 / 100
			set damage = rand(stren - damkoef, stren + damkoef)

			if damage <= 0:set damage = 1

			set koefatak = (speed + reakt + punch)
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="green">You apply a powerful punch.</font>'
			gs 'Kikatak', 'player'
			xgt 'kikbox', 'sta'
		end

		act 'Footed strike':
			cla
			set damkoef = stren * 20 / 100
			set damage = rand((stren - damkoef) * 3, (stren + damkoef) * 3)

			if damage <= 0:set damage = 1

			set koefatak = (speed + reakt + kik) / 2
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="green">You apply a kick powerful.</font>'
			gs 'Kikatak', 'player'
			xgt 'kikbox', 'sta'
		end
	end

	if hod = 1:
		!!Enemy attacks
		'<font color="red"><<$nameV>> attacks</font>'
		set tiprand = rand(0, 2)

		if tiprand = 0:
			set damage = rand(strenV / 12, strenV / 8)

			if damage <= 0:set damage = 1

			set koefatak = (speedV + reaktV + JabV) * 2
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="red"><<$nameV>> deals with quick jab step forward.</font>'
			gs 'Kikatak', 'enemy'
			xgt 'kikbox', 'sta'
		end

		if tiprand = 1:
			set damkoef = strenV * 20/100
			set damage = rand(strenV - damkoef, strenV + damkoef)

			if damage <= 0:set damage = 1

			set koefatak = (speedV + reaktV + punchV)
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="red"><<$nameV>> applies the power punch.</font>'
			gs 'Kikatak', 'enemy'
			xgt 'kikbox', 'sta'
		end

		if tiprand = 2:
			set damkoef = strenV * 20 / 100
			set damage = rand((strenV - damkoef) * 3, (strenV + damkoef) * 3)

			if damage <= 0:set damage = 1

			set koefatak = (speedV + reaktV + kikV) / 2
			set koefpop = koefatak * 20 / 100
			set popad = rand(koefatak - koefpop, koefatak + koefpop)
			'<font color="red"><<$nameV>> tries for a kick.</font>'
			gs 'Kikatak', 'enemy'
			xgt 'kikbox', 'sta'
		end
	end
end

if $ARGS[0] = 'sta':
	cla
	set ionce = 0
	'<b>==========================================================</b>'
	if minround = 1:'<b>the fight 1:00</b>'
	if minround = 2:'<b>the fight 1:30</b>'
	if minround = 3:'<b>the fight 2:00</b>'
	if minround = 4:'<b>the fight 2:30</b>'
	if minround = 5:'<b>the fight 3:00</b>'
	'<b>==========================================================</b>'
	'<b><<$nameV>></b> Life <b><font color="red"><<healthV>></font></b>, Endurance <b><font color="green"><<staminaV>></font></b>'
	'Your life <b><font color="red"><<health>></font></b>, Endurance <b><font color="green"><<stamina>></font></b>'
	'<b>==========================================================</b>'

	if health <= 0:
		'<b><font color="red">You lost by TKO</font></b>'
		gt 'enderKik', 'lossKO'
		exit
	end

	if healthV <= 0:
		'<b><font color="green"><<$nameV>> lost by TKO</font></b>'
		gt 'enderKik', 'winKO'
		exit
	end

	act 'Further':gt 'kikbox', 'fight'

	if minround = 5:
		cla
		*clr

		'Rang the gong indicating the end of the round <<round>>.'

		set minround = 0
		set round += 1

		act 'Back to the corner':
			cla
			*clr
			'<center><img src="images/community/kickboxing_corner.jpg"></center>'
			'You try to rest as best as you can.'

			act 'Prepare':
				if round <= formula:
					cla
					*clr
					'<center><b>Round <<round>></b></center>'
					*nl
					'<center><img src="images/community/ring<<round>>.jpg"></center>'

					act 'In the ring':gt 'kikbox', 'fight'
				else
					if pointKik > pointKikV:
						gt 'enderKik', 'win'
					end

					if pointKik < pointKikV:
						gt 'enderKik', 'loss'
					end

					if pointKik = pointKikV:
						gt 'enderKik', 'draw'
					end
				end
			end
		end
	end
end

--- kikbox ---------------------------------

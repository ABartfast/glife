# atakA

if $ARGS[0] = 'atak':
	cla
	!!расчет попадания уворота
	set poSp = speed * 20/100
	set uvAg = agilV * 20/100
	set poBox = react * 20/100
	set uvBox = reactV * 20/100
	set popad = rand(react - poBox, react + poBox) + rand(speed - poSp, speed + poSp) + bonusSh
	set uvorot = rand(reactV - uvBox, reactV + uvBox) + rand(agilV - uvAg, agilV + uvAg)
	set uvorKoef = uvorot * 50/100
	set popKoef = popad * 50/100

	if popad > uvorot:
		if popad >= uvorot + uvorKoef:
			!!крит, очень точное попадание
			'<b><font color="green">critical hits</font></b>'
			xgt 'atakA', 'uronKrit'
		end

		if popad < uvorot + uvorKoef:
			!!нормальное попадание
			!!атаковал противник
			'<b><font color="green">Contact</font></b>'
			xgt 'atakA', 'uron'
		end
	end

	if popad <= uvorot:
		'<b><font color="red">is washed</font></b>'
		xgt 'fight', 'sta'
	end
end

if $ARGS[0] = 'uronKrit':
	cla
	set damKoef = damTip * 20 / 100

	set damage = rand(damTip + damKoef, damTip + damKoef * 2)

	if damTipM > 0:
		set damKoefM = damTipM * 20/100
		set damageM = rand(damTipM + damKoefM, damTipM + damKoefM * 2)
	end

	xgt 'atakA', 'damage'
end

if $ARGS[0] = 'uron':
	cla
	set damKoef = damTip * 20 / 100
	set damage = rand(damTip - damKoef, damTip + damKoef)

	if damTipM > 0:
		set damKoefM = damTipM * 20/100
		set damageM = rand(damTipM - damKoefM, damTipM + damKoefM)
	end

	xgt 'atakA', 'damage'
end

if $ARGS[0] = 'damage':
	cla

	if defenceV <= 0 and klonV <= 0 and unmaterialV <= 0 and barierV <= 0:
		if dospehV > 0:set damageV -= dospehDefV & set dospehEn -= 1
		if damage < 0:set damage = 0

		set healthV -= damage

		if damTipM > 0:
			set mannaV -= damageM
			set manna += damageM
			'<b><font color="green">you have ingested <<damageM>> units of manna from the enemy.</font></b>'
		end

		!!set manna -= damageM

		if eleSh > 0:
			set stunV = rand(eleSh, eleSh * 3)
			'<b><font color="green"><<$nameV>> paralyzed <<stunV>> rounds</font></b>'
		end

		'<b><font color="green">You have caused damage <<damage>> units</font></b>'
		!!'<b><font color="red">caused damage your manna <<damageM>> units</font></b>'

		if zerkaloV > 0:
			set zerkaloV -= 1
			set health -= damage
			'<b><font color="red"><<$nameV>> reflected the damage and you got <<damage>> points of damage</font></b>'
		end

		xgt 'fight', 'sta'
	end

	if defenceV > 0 and klonV <= 0 and unmaterialV <= 0:
		!!set damSum = damage + damageM
		!!set defence -= damSum
		set defenceV -= damage
		'<b><font color="green">You have caused damage <<damage>> Protection of the enemy units</font></b>'
		xgt 'fight', 'sta'
	end

	if klonV > 0 and unmaterialV <= 0:
		set klonV -= 1
		'<b><font color="green">Destroyed enemy clone</font></b>'
		xgt 'fight', 'sta'
	end

	if unmaterialV > 0:
		set unmaterialV -= 1
		'<b><font color="red">Your shot went through the opponent´s body. Opponents do not materials</font></b>'
		xgt 'fight', 'sta'
	end

	if barierV > 0:
		!!барьер именитета для урона
		if barierV >= damage:
			'<b><font color="red"><<$nameV>> easily withstood your butt</font></b>'
			xgt 'fight', 'sta'
		end

		if barierV < damage:
			!!set manna -= damageM
			!!set health -= damage
			set damage -= barierV
			set healthV -= damage

			if damTipM > 0:
				set mannaV -= damageM
				set manna += damageM
				'<b><font color="green">you have ingested <<damageM>> units of manna from the enemy.</font></b>'
			end

			if eleSh > 0:
				set stunV = rand(eleSh, eleSh * 3)
				'<b><font color="green"><<$nameV>> paralyzed <<stunV>> rounds</font></b>'
			end

			'<b><font color="green">You have caused damage <<damage>> units</font></b>'
			!!'<b><font color="red">caused damage your manna <<damageM>> units</font></b>'

			if zerkaloV > 0:
				set zerkaloV -= 1
				set health -= damage
				'<b><font color="red">Enemy reflected damage and you got <<damage>> points of damage</font></b>'
			end
		end

		xgt 'fight', 'sta'
	end
end

if $ARGS[0] = 'atakW':
	cla
	!!расчет попадания уворота
	set poSp = speed * 20 / 100
	set uvAg = agilV * 20 / 100
	set poBox = react * 20 / 100
	set uvBox = reactV * 20 / 100
	set popad = rand(react - poBox, react + poBox) + rand(speed - poSp, speed + poSp)
	set uvorot = rand(reactV - uvBox, reactV + uvBox) + rand(agilV - uvAg, agilV + uvAg)
	set uvorKoef = uvorot * 50 / 100
	set popKoef = popad * 50 / 100

	if popad > uvorot:
		if popad >= uvorot + uvorKoef:
		!!крит, очень точное попадание
			'<b><font color="red">critical hits</font></b>'
			xgt 'atakA', 'uronKritW'
		end

		if popad < uvorot + uvorKoef:
			!!нормальное попадание
			'<b><font color="red">Contact</font></b>'
			xgt 'atakA', 'uronW'
		end
	end

	if popad <= uvorot:
		'<b><font color="green">is washed</font></b>'
		xgt 'fight', 'sta'
	end
end

if $ARGS[0] = 'uronKritW':
	cla
	set damKoef = damTip * 20 / 100
	set damage = rand(damTip + damKoef, damTip + damKoef * 2)
	set damKoefM = damTipM * 20 / 100
	set damageM = rand(damTipM + damKoefM, damTipM + damKoefM * 2)
	xgt 'atakA', 'damageW'
end

if $ARGS[0] = 'uronW':
	cla
	set damKoef = damTip * 20 / 100
	set damage = rand(damTip - damKoef, damTip + damKoef)
	set damKoefM = damTipM * 20 / 100
	set damageM = rand(damTipM - damKoefM, damTipM + damKoefM)
	xgt 'atakA', 'damageW'
end

if $ARGS[0] = 'damageW':
	cla

	if defenceV <= 0 and klonV <= 0 and unmaterialV <= 0 and barierV <= 0:
		if dospehV > 0:set damageV -= dospehDefV & set dospehEn -= 1
		if damage < 0:set damage = 0

		set healthV -= damage
		set mannaV -= damageM
		'<b><font color="green">You are harmed <<damage>> units</font></b>'
		'<b><font color="green">You have caused damage to the enemy manna <<damageM>> units</font></b>'

		if zerkaloV > 0:
			set zerkaloV -= 1
			set health -= damage
			'<b><font color="red"><<$nameV>> reflected the damage and you got <<damage>> points of damage</font></b>'
		end

		xgt 'fight', 'sta'
	end

	if defenceV > 0 and klonV <= 0 and unmaterialV <= 0:
		set damSum = damage + damageM
		set defenceV -= damSum
		!!set defenceV -= damage
		'<b><font color="green">You have caused damage <<damSum>> Protection of the enemy units</font></b>'
		xgt 'fight', 'sta'
	end

	if klonV > 0 and unmaterialV <= 0:
		set klonV -= 1
		'<b><font color="green">Destroyed enemy clone</font></b>'
		xgt 'fight', 'sta'
	end

	if unmaterialV > 0:
		set unmaterialV -= 1
		'<b><font color="red">Your shot went through the opponent´s body. Opponents do not materials</font></b>'
		xgt 'fight', 'sta'
	end

	if barierV > 0:
		!!барьер именитета для урона
		if barierV >= damage:
			'<b><font color="red"><<$nameV>> easily withstood your butt</font></b>'
			xgt 'fight', 'sta'
		end

		if barierV < damage:
			set mannaV -= damageM
			set healthV -= damage
			!!set healthV -= damage
			'<b><font color="green">You have caused damage <<damage>> units</font></b>'
			'<b><font color="green">You have caused damage to the enemy manna <<damageM>> units</font></b>'

			if zerkaloV > 0:
				set zerkaloV -= 1
				set health -= damage
				'<b><font color="red">Enemy reflected damage and you got <<damage>> points of damage</font></b>'
			end
		end

		xgt 'fight', 'sta'
	end
end

--- atakA ---------------------------------
